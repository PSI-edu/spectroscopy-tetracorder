#!/usr/local/bin/davinci -f
#####!/usr/bin/env -S davinci -f  

###########################  was:  #!/usr/local/bin/davinci -f



# A master color set.  Just read in the files, assign the colors
# and write the results.    R. Clark 4/16/2015 - 11/16/2015

# command line:
# prog    gray_base_image  prefix-file-name   [dual|nodual]

prog="davinci.make.veg-water-rgb"     # CHANGE THIS if program name changes

####################  Begin static: no need to change inything in this section ################

if ($argc < 2) {

	printf("Insufficient input\n")
	printf("%s input_gray_image  outputcolorimage\n", prog)
	printf("exit 1\n")
	exit(1)
}


dual    = 1     # default color + gray image 
dualmax = 2000  # no dual if width is greater than this

if ($argc >= 3) {

	if ( "$3" == "nodual" ) {
		dual = 0
	}
	if ( "$3" == "dual" ) {
		dual = 1
		dualmax = 999999 
	}
}

# read base gray image

gbase=read(filename=$1)
if (HasValue(gbase) ==0) {                   # check that read worked
      printf ("read ERROR on file: %s\n",$1)
      printf ("^GEXIT 1\n")
      exit(1)
}

# execute this program in the same directory asthe source code below
source ("cmds.color.support/davinci.master.colors")

# 22 available colors:
# c_red      c_green          c_blue       c_yellow        c_magenta       c_cyan      c_armygreen  
# c_salmon   c_flesh          c_orange     c_deeppink      c_yellowgreen   c_lavender  c_bluegreen
# c_skyblue  c_paleseagreen   c_white      c_redpink       c_gold          c_mudyellow c_lightpink
# c_purple


# a = first input image, 3-band color, xcolor = the color output image, RGB float
# colorout = the color output image, 3-byte RGB


define read_an_image(1,1) {

	# read a gray-scale image and convert to 3-bands

	afile = $1   # file to read
	printf ("reading file %s\n", afile)
	aaa=read(filename=afile)
	if (HasValue(aaa) ==0) {  # check that read worked
		printf ("read ERROR on file: %s\n",afile)
		printf ("setting to zero\n")
		aaa=0
	}
	qqq = cat(aaa,aaa,axis=z)  # make into 2-band image
	aaa = cat(aaa,qqq,axis=z)  # make into 3-band image

	return (aaa)

}  # end define



###################   begin custom section ##########################################

# first image:

# c_white
a = read_an_image("group.2um/carbonate_calcite.fd.gif")    # change this to read an images so we can get dimensions.

	# set up output image xcolor:
	xcolor=float(a*0)
	xdim=dim(a)

	# now add the colors

# 22 available colors:
# c_red      c_green          c_blue       c_yellow        c_magenta       c_cyan      c_armygreen  
# c_salmon   c_flesh          c_orange     c_deeppink      c_yellowgreen   c_lavender  c_bluegreen
# c_skyblue  c_paleseagreen   c_white      c_redpink       c_gold          c_mudyellow c_lightpink
# c_purple


# first image:


# make an RGB images from 3 water bands:

# group.veg//veg0.9um.band.fd.gif   #   red
# group.veg//veg1.2um.band.fd.gif   #   green
# group.veg//veg1.4um.band.fd.gif   #   blue



r=   read_an_image("group.veg//veg0.9um.band.fd.gif")
g=   read_an_image("group.veg//veg1.2um.band.fd.gif")
b=   read_an_image("group.veg//veg1.4um.band.fd.gif")

r[,,2] = r[,,2] * 0
r[,,3] = r[,,3] * 0

g[,,1] = g[,,1] * 0
g[,,3] = g[,,3] * 0

b[,,1] = b[,,1] * 0
b[,,2] = b[,,2] * 0

xcolor = r *2.0 + g * 1.5 + b

########### end custom, no need to change anything below this line ###############################

xout=byte(xcolor+0.5)   # output color image, byte
xout = bip(xout)
xdim=dim(xout)
xdimx=xdim[1,,]      # color image width

gbasedim=dim(gbase)
x=gbasedim[1,,]      # base image width

if (x > dualmax) {
	dual = 0
}

if ( dual == 1 ) {
	
	gbase = bip(gbase)
	xout = cat (xout, gbase, axis=x)
}


fout=$2


adim=dim(xout)
if ( adim[1,,] > 5 || adim[2,,] > 5 ) {
	printf ("writing %s\n", fout)
	write (xout, filename=fout, type=png)
} else {
	printf ("not writing %s\n", fout)
	printf ("    image size < 6 pixels\n")
}
