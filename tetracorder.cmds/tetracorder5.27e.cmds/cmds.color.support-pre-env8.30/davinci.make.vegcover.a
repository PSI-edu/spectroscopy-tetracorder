#!/usr/local/bin/davinci -f
#####!/usr/bin/env -S davinci -f  

###########################  was:  #!/usr/local/bin/davinci -f



# command line:
# prog    gray_base_image  prefix-file-name   [dual|nodual]


dual    = 1     # default color + gray image 
dualmax = 2000  # no dual if width is greater than this

if ($argc >= 3) {

	if ( "$3" == "nodual" ) {
		dual = 0
	}
	if ( "$3" == "dual" ) {
		dual = 1
		dualmax = 999999 
	}
}


gbase=read(filename=$1)
if (HasValue(gbase) ==0) {                   # check that read worked
      printf ("read ERROR on file: %s\n",$1)
      printf ("^GEXIT 1\n")
      exit(1)
}


define read_an_image(1,1) {

	# read a gray-scale image and convert to 3-bands

	afile = $1   # file to read
	printf ("reading file %s\n", afile)
	aaa=read(filename=afile)
	if (HasValue(aaa) ==0) {  # check that read worked
		printf ("read ERROR on file: %s\n",afile)
		printf ("setting to zero\n")
		aaa=0
	}
	qqq = cat(aaa,aaa,axis=z)  # make into 2-band image
	aaa = cat(aaa,qqq,axis=z)  # make into 3-band image

	return (aaa)

}  # end define

# group.veg/f130924t01p00r10rdn_refl_img_corr-bil20k.v.ch20.base.jpg.base.jpg
# group.veg/veg0.9um.band.fd.gif
# group.veg/veg1.2um.band.fd.gif
# group.veg/veg1.4um.band.fd.gif

a = read_an_image("group.veg/vegetation.map.fd.gif")

b = read_an_image("group.veg/vegetation.weak.map.fd.gif")

c = read_an_image("group.veg/vegetation.dry+green.fd.gif")

d = read_an_image("group.veg/vegetation.dry_nonphotosyn.fd.gif")

# /info/COLORS.screen.to.printers
#
#      "color"         r    g    b
#    1 red            255   60    0   (255   50    0 better)
#    2 green           60  255    0
#    3 blue             0  105  255
#   
#    4 yellow         255  255    0
#    5 magenta        205   60  255
#    6 cyan             0  255  255
#  
#    7 army green     150  165    0   (160  175    0 better?)
#    8 salmon         255  170  170
#    9 flesh          255  200  155
#  
#   10 orange         255  110    0   (255  140    0 better?)
#   11 deep pink      235    0  180
#   12 yellow green   190  255    0
#
#   13 lavender       185  120  255
#   14 sky blue       140  210  255
#   15 pale sea green 110  255  190
#  
#   16 white          255  255  255
#   17 red-pink       255  125  125
#   18 gold           255  213    0
#
#   19 mud yellow     186  165    0
#   20 light pink     255  211  255
#

argb =  create(1,1,3, format=FLOAT, start=0.0, step=0.0)
brgb =  create(1,1,3, format=FLOAT, start=0.0, step=0.0)
crgb =  create(1,1,3, format=FLOAT, start=0.0, step=0.0)
drgb =  create(1,1,3, format=FLOAT, start=0.0, step=0.0)

# Colors:
#              Red                  Green                Blue
argb[1,1,1] =   60. ; argb[1,1,2]=   255. ;  argb[1,1,3]=   0.   # green         vegetation.map
brgb[1,1,1] =  150. ; brgb[1,1,2]=   165. ;  brgb[1,1,3]=   0.   # army green    vegetation.weak.map

crgb[1,1,1] =  190. ; crgb[1,1,2]=   255. ;  crgb[1,1,3]=   0.   # yellow green  vegetation.dry+green
drgb[1,1,1] =  255. ; drgb[1,1,2]=   255. ;  drgb[1,1,3]=   0.   # yellow        dry_nonphotosyn


xaz=float(a*0)

xaz= xaz + a*(argb/255.)
xaz= xaz + b*(brgb/255.)
xaz= xaz + c*(crgb/255.)
xaz= xaz + d*(drgb/255.)

xout=byte(xaz+0.5)
xout = bip(xout)

gbasedim=dim(gbase)
x=gbasedim[1,,]

if (x > dualmax) {
	dual = 0
}

if ( dual == 1 ) {
	
	gbase = bip(gbase)
	xout = cat (xout, gbase, axis=x)
}



fout=$2


adim=dim(xout)
if ( adim[1,,] > 5 || adim[2,,] > 5 ) {
	printf ("writing %s\n", fout)
	write (xout, filename=fout, type=png)
} else {
	printf ("not writing %s\n", fout)
	printf ("    image size < 6 pixels\n")
}
